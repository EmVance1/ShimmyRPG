
entry(BigbordIntroScene.IsActive & once) = BertPrime: [ "Oh hello there stranger. Fancy a game?" ] => {
    "How inviting, I'd love to." => v01,
    "If that's what counts for fun around here... Stranger." => v02{ Bert.Mistrust += 1 },
}

entry(default) = BertPrime: [ "Lovely spot this, don't you think?" ] => exit

v01 = BertPrime: [ "Good on ye. You look like someone open to new experiences.", "It'll get you a long way around here." ] => level1_pick
v02 = BertPrime: [ "You need to open yourself up to new experiences friend.", "It'll get you a long way around here." ]    => level1_pick

level1_pick = BertPrime: [ "Why don't you pick a card, any card." ] => {
    (_pick:rng3 == 0)                      "*Pick a card*" => level1_star,
    (_pick      == 1 & Bert.Mistrust == 0) "*Pick a card*" => level1_moon,
    (_pick      == 1 & Bert.Mistrust >  0) "*Pick a card*" => level1_moon_sus,
    (_pick      == 2)                      "*Pick a card*" => level1_sun,
}

level1_star = Narrator:  [ "*TEMPERANCE*" ] =>
              BertPrime: [ "Are you one to rush into things perhaps? Could you benefit from some... Temperance?" ] => level2_pick

level1_moon = Narrator:  [ "*THE MOON*" ] =>
              BertPrime: [ "Fascinating. You struck me rather as the more trusting type." ] => level2_pick

level1_moon_sus = Narrator:  [ "*THE MOON*" ] =>
                  BertPrime: [ "The moon. The cards have heard your distrust. You shan't need it here." ] => level2_pick

level1_sun = Narrator:  [ "*THE SUN*" ]  =>
             BertPrime: [ "Aa the sun. May a such a great light indeed lie in your future." ] => level2_pick


level2_pick = BertPrime: [ "Go on, have another." ] => {
    (_pick:rng3 == 0) "*Pick another card*" => level2_tower,
    (_pick      == 1) "*Pick another card*" => level2_justice,
    (_pick      == 2) "*Pick another card*" => level2_lovers,
}

level2_tower = Narrator:  [ "*THE TOWER*" ] =>
               BertPrime: [ "Yes, yes. Something dark lies in your future. Very dark indeed." ] => level3_pick

level2_justice = Narrator:  [ "*JUSTICE*" ] =>
                 BertPrime: [ "Order. You seek order. No... you seek to understand." ] => level3_pick

level2_lovers = Narrator:  [ "*THE LOVERS*" ] =>
                BertPrime: [ "Hmm, a tricky one. Perhaps you will be entering into new communities very soon." ] => level3_pick


level3_pick = BertPrime: [ "Just one more. Indulge me, please." ] => {
    "*Pick a final card*" => level3,
}

level3 = Narrator:  [ "*THE HANGED MAN*" ] =>
         BertPrime: [ "..", "....." ] =>
         Narrator:  [ "*Seemingly out of nowhere, Bert hastily packs away the cards*" ] =>
         BertPrime: [ "Bunch of superstitious hokum anyways.", "Say friend, do you already have a place to stay tonight?" ] => {
    ( Player.Lodged) "That big bird offered me a place. Might take him up on it." => sus4,
    (!Player.Lodged) "Not as of yet." => bigsus,
}

sus4   = BertPrime: [ "Splendid splendid. Well, I'll be seeing you around then." ] => exit
bigsus = BertPrime: [ "Oh well then I must insist you stay with me. It's my pleasure, truly." ] => {
    "I could never impose that way. Y'know you'd think a place like this has rooms." => sus5,
    "What a generous offer. I just might take you up on it."
        => bigsus2{ Bert.LodgedPlayer = true, Bert.MoveToShack = true, Bert.FrontDoorLocked = false },
}

sus5 = BertPrime: [ "But of course it does. Just ask Big Bird. But I'm sure we'll be seeing each other again. Farewell." ] => exit
bigsus2 = BertPrime: [ "Splendid splendid. Come to my lodge down the street." ] => exit_with{ "('OnBertExitShack', {})" }

